(**************************************************************)
(*   Copyright Dominique Larchey-Wendling [*]                 *)
(*                                                            *)
(*                             [*] Affiliation LORIA -- CNRS  *)
(**************************************************************)
(*      This file is distributed under the terms of the       *)
(*        Mozilla Public License Version 2.0, MPL-2.0         *)
(**************************************************************)

From Coq
  Require Import Arith List Lia Eqdep_dec Utf8.

From Coq
  Require Vector.

From KruskalTrees
  Require Import notations tactics idx.

Set Implicit Arguments.

Import idx_notations.

#[local] Reserved Notation "v ‚¶É p ‚¶Ñ" (at level 1, format "v ‚¶É p ‚¶Ñ").
#[local] Reserved Notation "x '##' y" (at level 60, right associativity).
#[local] Reserved Notation "x '‚àà·µ•' v" (at level 70, format "x  ‚àà·µ•  v").
#[local] Reserved Notation "'‚¶ë' i ',' v '‚¶í'"  (at level 0, format "‚¶ë i , v ‚¶í").

Notation vec := Vector.t.
Notation vec_nil := Vector.nil.
Notation vec_cons := Vector.cons.

Arguments vec_nil {A}.
Arguments vec_cons {A} _ {n}.

#[local] Notation "‚àÖ" := vec_nil.
#[local] Infix "##" := vec_cons.

Create HintDb vec_db.

Section vec_invert.

  Variable X : Type.

  (** General induction principle for vec 0 and vec (S n).
      Following Smaller inversions by JF Monin (TYPES 2022)
    *)

  Inductive vec_shape_O : vec X 0 ‚Üí Set :=
    | vec_shape_O_nil : vec_shape_O ‚àÖ.

  Inductive vec_shape_S {n} : vec X (S n) ‚Üí Type :=
    | vec_shape_S_cons x v : vec_shape_S (x##v).

  Definition vec_invert_t {n} : vec X n ‚Üí Type :=
    match n with
    | 0   => vec_shape_O
    | S n => vec_shape_S
    end.

  Definition vec_invert {n} v : @vec_invert_t n v :=
    match v return vec_invert_t v with
    | ‚àÖ    => vec_shape_O_nil
    | x##v => vec_shape_S_cons x v
    end.

  Definition vec_head n (v : vec X (S n)) :=
    match vec_invert v with
      | vec_shape_S_cons x _ => x
    end.

  Definition vec_tail n (v : vec X (S n)) :=
    match vec_invert v with
    | vec_shape_S_cons _ w => w
    end.

  Fact vec_head_tail n (v : vec X (S n)) : v = vec_head v##vec_tail v.
  Proof. now destruct (vec_invert v). Qed.

End vec_invert.

Arguments vec_head {_ _} _ /.
Arguments vec_tail {_ _} _ /.

Tactic Notation "vec" "invert" hyp(v) :=
  destruct (vec_invert v).

Tactic Notation "vec" "invert" hyp(v) "as" simple_intropattern(x) ident(w) :=
  destruct (vec_invert v) as [ x w ].

Section vec_in.

  Variable X : Type.

  (** vector membership, via an inductive definition, to allow for
      nesting in other induction definitions (strict positivity) *)

  Inductive vec_in x : ‚àÄn, vec X n ‚Üí Prop :=
    | vec_in_hd n (v : vec _ n)   :          x ‚àà·µ• x##v
    | vec_in_tl n y (v : vec _ n) : x ‚àà·µ• v ‚Üí x ‚àà·µ• y##v
  where "x ‚àà·µ• v" := (vec_in x v).

  Hint Constructors vec_in : core.

  Fact vec_in_inv x n (v : vec _ n) :
         x ‚àà·µ• v
       ‚Üí match v with
         | ‚àÖ    => False
         | y##v => x = y \/ x ‚àà·µ• v
         end.
  Proof. induction 1; auto. Qed.

  Fact vec_in_nil_inv x (v : vec X 0) : x ‚àà·µ• v ‚Üî False.
  Proof. split; inversion 1. Qed.

  Fact vec_in_cons_inv x y n (v : vec _ n) : x ‚àà·µ• y##v ‚Üî x = y ‚à® vec_in x v.
  Proof.
    split.
    + apply vec_in_inv.
    + intros [ <- | ]; constructor; auto.
  Qed.

End vec_in.

Arguments vec_in {_} _ {_} _.

#[local] Notation "x ‚àà·µ• v" := (vec_in x v).
#[global] Hint Constructors vec_in : vec_db.

(* ùïÜùïä Œª ‚àÄ‚àÉ ‚Üí ‚Üî ‚àß ‚à® *)

Section vec_prj.

  Variable (X : Type).

  (** A nice implementation is *critical* here when using nested defs
      Notice that idx_O_rect is implemented with

                   match _ : Empty_set with end

      which allows vec_prj v p to be recognized as a sub-term of v *)

  Fixpoint vec_prj n (v : vec _ n) : idx n ‚Üí X :=
    match v with
    | ‚àÖ    => idx_O_rect _
    | x##v => Œª i,
      match idx_S_inv i with
      | None   => x
      | Some j => v‚¶Éj‚¶Ñ
      end
    end
  where "v ‚¶É i ‚¶Ñ" := (@vec_prj _ v i).

  (** The following fixpoint equations hold *by reduction*
      and v‚¶Éi‚¶Ñ is effectivelly recognised as a sub-term of v
      by the guardness checker of Coq, so you can implement
      recursive fixpoint calls on these, for instance
      using vec in trees. *)

  Goal ‚àÄn x (v : vec _ n),   (x##v)‚¶ÉùïÜ‚¶Ñ   = x.   Proof. reflexivity. Qed.
  Goal ‚àÄn x (v : vec _ n) i, (x##v)‚¶Éùïä i‚¶Ñ = v‚¶Éi‚¶Ñ. Proof. reflexivity. Qed.

  (** unlike idx n ‚Üí X, vectors in vec X n are equal
      iff there components are *)

  Fact vec_prj_ext n (v w : vec X n) : (‚àÄp, v‚¶Ép‚¶Ñ = w‚¶Ép‚¶Ñ) ‚Üí v = w.
  Proof.
    revert v w; induction n as [ | n IHn ]; intros v w.
    + vec invert v; vec invert w; trivial.
    + vec invert v as x v.
      vec invert w as y w.
      intros H; f_equal.
      * apply (H ùïÜ).
      * apply IHn; intros; apply (H (ùïä _)).
  Qed.

  Variable (n : nat).

  Fact vec_prj_fix_1 (v : vec X (S n)) : v‚¶ÉùïÜ‚¶Ñ = vec_head v.
  Proof. now vec invert v as ? v. Qed.

  Fact vec_prj_fix_2 (v : vec X (S n)) p : v‚¶Éùïä p‚¶Ñ = (vec_tail v)‚¶Ép‚¶Ñ.
  Proof. now vec invert v as ? v. Qed.

  Definition vec_nxt := vec_prj_fix_2.

  Fact vec_in_iff_prj x v : @vec_in X x n v ‚Üî ‚àÉp, x = v‚¶Ép‚¶Ñ.
  Proof.
    split.
    + induction 1 as [ n v | n y v _ (p & Hp) ].
      * now exists ùïÜ.
      * now exists (ùïä p).
    + intros (p & ->).
      revert v p; induction n; intros v p;
        idx invert p; vec invert v as ? v; simpl; eauto with vec_db.
  Qed.

End vec_prj.

Arguments vec_prj {X n}.
#[local] Notation "v ‚¶É i ‚¶Ñ" := (vec_prj v i).

Tactic Notation "vec" "ext" "with" ident(p) :=
  apply vec_prj_ext; intros p.

Tactic Notation "vec" "ext" :=
  apply vec_prj_ext; intro.

Section vec_basics.

  Variable X : Type.

  (** Inversion lemmas for v _ 0 and vec _ (S n)

      Beware that the vec invert tactic is the preferred choice
   *)

  Fact vec_0_inv (v : vec X 0) : v = ‚àÖ.
  Proof. now vec invert v. Qed.

  Fact vec_S_inv n (v : vec X (S n)) : { x : _ & { w | v = x##w } }.
  Proof. now vec invert v as x w; eauto. Qed.

  (** vec_cons is injective *)

  Fact vec_cons_inj n x y (v w : vec X n) : x##v = y##w ‚Üí x = y ‚àß v = w.
  Proof.
     intros H; split.
     + now apply f_equal with (f := vec_head) in H.
     + now apply f_equal with (f := vec_tail) in H.
  Qed.

  (**    vec_prj : vec X n ‚Üí (idx n ‚Üí X)
     and vec_set : (idx n ‚Üí X) ‚Üí vec X n *)

  Fixpoint vec_set n : (idx n ‚Üí X) ‚Üí vec X n :=
    match n return (idx n ‚Üí _) ‚Üí vec _ n with
      | 0   => Œª _, ‚àÖ
      | S n => Œª f, f ùïÜ##vec_set (fun p => f (ùïä p))
    end.

  Fact vec_prj_set n (f : idx n ‚Üí X) p : (vec_set f)‚¶Ép‚¶Ñ  = f p.
  Proof.
    revert f p; induction n as [ | n IHn ]; intros g p; idx invert p; auto.
    apply IHn.
  Qed.

  Fact vec_set_prj n v : @vec_set n (vec_prj v) = v.
  Proof. now vec ext; rewrite vec_prj_set. Qed.

  (** From vectors to lists *)

  Fixpoint vec_list n (v : vec X n) : list X :=
    match v with
      | ‚àÖ    => nil
      | x##v => x::vec_list v
    end.

  Fact vec_list_length n v : length (@vec_list n v) = n.
  Proof. induction v; simpl; f_equal; auto. Qed.

  Fact vec_list_idx n (v : vec X n) : vec_list v = map (vec_prj v) (idx_list n).
  Proof. induction v; simpl; f_equal; rewrite map_map; auto. Qed.

  Fact in_vec_list n v x : In x (@vec_list n v) ‚Üî ‚àÉi, v‚¶Éi‚¶Ñ = x.
  Proof.
    induction v as [ | n y v IH ]; simpl.
    * split; try tauto; intros (p & _); idx invert p.
    * split.
      + intros [ H | H ].
        - exists idx_fst; auto.
        - rewrite IH in H.
          destruct H as (p & Hp); exists (idx_nxt p); auto.
      + intros (p & Hp); idx invert p; auto.
        rewrite IH; firstorder.
  Qed.

  Fixpoint list_vec l : vec X ‚åäl‚åã :=
    match l return vec X ‚åäl‚åã with
      | nil  => ‚àÖ
      | x::l => x##list_vec l
    end.

  Fact vec_list_vec l : vec_list (list_vec l) = l.
  Proof. induction l; simpl; f_equal; auto. Qed.

  Fact list_vec_sig l : { n : nat & { v : vec _ n | vec_list v = l } }.
  Proof. exists (length l), (list_vec l); apply vec_list_vec. Qed.

  Fact list_vec_list n v : ‚àÉe, v‚Ü∫e = list_vec (@vec_list n v).
  Proof.
    induction v as [ | n x v (e & IHv) ]; simpl.
    + exists eq_refl; auto.
    + revert e IHv.
      generalize (vec_list v).
      intros l ->; simpl; intros ->.
      exists eq_refl; auto.
  Qed.

  Fact vec_list_split_inv n v l x r :
        @vec_list n v = l++x::r ‚Üí { i | v‚¶Éi‚¶Ñ = x /\ idx2nat i = ‚åäl‚åã }.
  Proof.
    revert l x r.
    induction v as [ | n x v IHv ].
    + now intros [] ? ?.
    + intros [ | y l ] z r; simpl.
      * inversion 1; exists idx_fst; auto.
      * intros H; injection H; clear H; intros H1 <-.
        apply IHv in H1 as (p & H1 & H2).
        exists (idx_nxt p); split; simpl; auto.
  Qed.

  (** vector append *)

  Fixpoint vec_app n (v : vec X n) m (w : vec X m) : vec X (n+m) :=
    match v with
      | ‚àÖ    => w
      | x##v => x##vec_app v w
    end.

  Fact vec_app_idx_left n v m w p : (@vec_app n v m w)‚¶Éidx_left _ p‚¶Ñ = v‚¶Ép‚¶Ñ.
  Proof. induction v; idx invert p; auto. Qed.

  Fact vec_app_idx_right n v m w p : (@vec_app n v m w)‚¶Éidx_right _ p‚¶Ñ = w‚¶Ép‚¶Ñ.
  Proof. induction v; simpl; auto. Qed.

End vec_basics.

#[export] Hint Rewrite vec_prj_set vec_app_idx_left vec_app_idx_right : vec_db.

Tactic Notation "vec" "rew" := autorewrite with vec_db.

(* ùïÜùïä Œª ‚àÄ‚àÉ ‚Üí ‚Üî ‚àß ‚à® *)

Section vec_map.

  Variables (X Y : Type) (f : X ‚Üí Y).

  Fixpoint vec_map n (v : vec X n) : vec Y n :=
    match v with
      | ‚àÖ    => ‚àÖ
      | x##v => f x##vec_map v
    end.

  Fact vec_prj_map n v : ‚àÄi : idx n, (vec_map v)‚¶Éi‚¶Ñ = f v‚¶Éi‚¶Ñ.
  Proof. induction v; intro; idx invert all; auto. Qed.

  Fact vec_list_map n v : vec_list (@vec_map n v) = map f (vec_list v).
  Proof. induction v; simpl; f_equal; auto. Qed.

End vec_map.

#[export] Hint Rewrite vec_prj_map : vec_db.

Arguments vec_map {_ _} _ {_} _.

Fact vec_set_map X Y f n v : vec_set (fun i => f v‚¶Éi‚¶Ñ) = @vec_map X Y f n v.
Proof. now vec ext; vec rew. Qed.

Fact vec_map_map X Y Z (f : X -> Y) (g : Y -> Z) n (v : vec X n) :
    vec_map g (vec_map f v) = vec_map (fun x => g (f x)) v.
Proof. induction v; simpl; f_equal; auto. Qed.

Section lvec_hvec.

  Variable X : Type.

  (* isomorphic to list X but with length pre-computed *)

  Inductive lvec : Type :=
    | lvec_pack n : vec X n ‚Üí lvec.

  Definition lvec_len l :=
    match l with
      | @lvec_pack n _ => n
    end.

  Definition lvec_vec l : vec _ (lvec_len l) :=
    match l with
      | lvec_pack v => v
    end.

  Fact lvec_pair_eq l : l = @lvec_pack (lvec_len l) (lvec_vec l).
  Proof. destruct l; auto. Qed.

  Definition hvec := vec lvec.

  Fixpoint hvec_size n (w : hvec n) :=
    match w with
      | ‚àÖ => 0
      | v##w => 1+(lvec_len v+hvec_size w)
    end.

  Fact hvec_size_ge n w : n <= @hvec_size n w.
  Proof. induction w as [  | [] ]; simpl; tlia. Qed.

End lvec_hvec.

Arguments lvec_pack {X} n v.

Module vec_notations.

  Notation "‚àÖ" := vec_nil.
  Notation "x ## v" := (vec_cons x v).
  Notation "v ‚¶É p ‚¶Ñ" := (vec_prj v p).
  Notation "x ‚àà·µ• v" := (@vec_in _ x _ v).
  Notation "‚¶ë i , v ‚¶í" := (lvec_pack i v).

End vec_notations.

Import vec_notations.

Theorem vec_eq_dec_ext X n (u v : vec X n) :
       (forall i, { u‚¶Éi‚¶Ñ = v‚¶Éi‚¶Ñ } + { u‚¶Éi‚¶Ñ <> v‚¶Éi‚¶Ñ })
    -> { u = v } + { u <> v }.
Proof.
  induction u as [ | x n u IH ] in v |- *.
  + vec invert v; eauto.
  + vec invert v as y v; intros H.
    destruct (H ùïÜ) as [ C | C ]; simpl in C; subst.
    2: now right; contradict C; apply vec_cons_inj in C.
    specialize (fun i => H (ùïä i)); simpl in H.
    apply IH in H as [ <- | C ]; auto.
    now right; contradict C; apply vec_cons_inj in C.
Qed.

Definition lvec_map {X Y} f v := match v with ‚¶ën,v‚¶í => ‚¶ën,@vec_map X Y f n v‚¶í end.

Section vec_reif.

  (** Finitary choice functions *)

  Variable (n : nat) (X : Type) (R : idx n ‚Üí X ‚Üí Prop).

  Fact vec_reif : (‚àÄp, ‚àÉx, R p x) ‚Üí ‚àÉv, ‚àÄp, R p v‚¶Ép‚¶Ñ.
  Proof.
    intros H; idx reif H as (f & Hf).
    exists (vec_set f).
    now intro; vec rew.
  Qed.

  Fact vec_reif_t : (‚àÄp, { x | R p x }) ‚Üí { v | ‚àÄp, R p v‚¶Ép‚¶Ñ }.
  Proof.
    intros H; idx reif H as (f & Hf).
    exists (vec_set f).
    now intro; vec rew.
  Qed.

  Definition vec_reif_tt : (‚àÄp, { x : _ & R p x }) ‚Üí { v | ‚àÄp, R p v‚¶Ép‚¶Ñ }.
  Proof. intro; apply vec_reif_t; firstorder. Qed.

End vec_reif.

Tactic Notation "vec" "reif" hyp(H) "as" simple_intropattern(P) :=
  match type of H with
  | ‚àÄ_ : idx _, ex _ => apply vec_reif in H as P
  | ‚àÄ_ : idx _, sig _ => apply vec_reif_t in H as P
  | ‚àÄ_ : idx _, sigT _ => apply vec_reif_tt in H as P
  end.

Section vec_cond_reif.

  Variable (X Y : Type) (P : Y ‚Üí Prop) (f : X ‚Üí Y)
           (P_inv : ‚àÄy, P y ‚Üí { x | f x = y}).

  Fact vec_cond_reif n (w : vec _ n) :
      (‚àÄi, P w‚¶Éi‚¶Ñ) ‚Üí { v | vec_map f v = w }.
  Proof.
    intros H.
    generalize (fun i => P_inv (H i)); intros H'.
    vec reif H' as (v & Hv).
    exists v.
    now vec ext; vec rew.
  Qed.

End vec_cond_reif.

Section vec_fall.

  Variable (X : Type) (P : X ‚Üí Prop).

  Definition vec_fall n (v : vec _ n) := ‚àÄi, P v‚¶Éi‚¶Ñ.

  Fact vec_fall_nil : vec_fall ‚àÖ.
  Proof. intro; idx invert all. Qed.

  Fact vec_fall_cons n x (v : vec _ n) : P x ‚Üí vec_fall v ‚Üí vec_fall (x##v).
  Proof. intros H1 H2 p; idx invert p; auto. Qed.

  Hint Resolve vec_fall_nil vec_fall_cons : core.

  Fact vec_fall_nil_iff (v : vec _ 0) : vec_fall v ‚Üî True.
  Proof. vec invert v; split; auto. Qed.

  Fact vec_fall_cons_iff n x (v : vec _ n) : vec_fall (x##v) ‚Üî P x ‚àß vec_fall v.
  Proof. unfold vec_fall; rewrite forall_idx_Sn; simpl; tauto. Qed.

  Section vec_fall_rect.

    Variables (Q : ‚àÄn, vec _ n ‚Üí Type)
              (Qnil : Q ‚àÖ)
              (Qcons : ‚àÄn x (v : vec _ n), vec_fall v ‚Üí Q v ‚Üí Q (x##v)).

    Theorem vec_fall_rect n (v : vec _ n) : vec_fall v -> Q v.
    Proof.
      induction v as [ | n x v IH ]; intros H; auto.
      apply vec_fall_cons_iff in H as []; auto.
    Qed.

  End vec_fall_rect.

End vec_fall.

#[global] Hint Resolve vec_fall_nil vec_fall_cons : vec_db.

Section vec_fall2.

  Variables (X Y : Type) (R : X ‚Üí Y ‚Üí Prop).

  Definition vec_fall2 n (u : vec _ n) v := ‚àÄi, R u‚¶Éi‚¶Ñ v‚¶Éi‚¶Ñ.

  Fact vec_fall2_nil (v w : vec _ 0) : vec_fall2 v w.
  Proof. intro; idx invert all. Qed.

  Fact vec_fall2_cons n x (v : vec _ n) y w :
        R x y ‚Üí vec_fall2 v w ‚Üí vec_fall2 (x##v) (y##w).
  Proof. intros ? ? p; idx invert p; auto. Qed.

  Hint Resolve vec_fall2_nil vec_fall2_cons : core.

  Fact vec_fall2_cons_inv n x (v : vec _ n) y w :
         vec_fall2 (x##v) (y##w) ‚Üî R x y ‚àß vec_fall2 v w.
  Proof.
    split.
    + intros H; split.
      * apply (H idx_fst).
      * intro; apply (H (idx_nxt _)).
    + intros []; apply vec_fall2_cons; auto.
  Qed.

  Section vec_fall2_rect.

    Variable (P : ‚àÄn, vec X n ‚Üí vec Y n ‚Üí Type)
             (HP0 : P ‚àÖ ‚àÖ)
             (HP1 : ‚àÄ n x y v w, R x y ‚Üí vec_fall2 v w ‚Üí @P n v w ‚Üí P (x##v) (y##w)).

    Fact vec_fall2_rect n : ‚àÄ v w : vec _ n, vec_fall2 v w ‚Üí P v w.
    Proof.
      induction n as [ | n IHn ]; intros v w.
      + vec invert v; vec invert w; auto.
      + vec invert v as x v; vec invert w as y w; intros Hw.
        apply vec_fall2_cons_inv in Hw as []; auto.
    Qed.

  End vec_fall2_rect.

End vec_fall2.

#[global] Hint Resolve vec_fall2_nil vec_fall2_cons : vec_db.

Fact vec_fall2_swap X Y R n u v :
     @vec_fall2 X Y R n u v ‚Üí vec_fall2 (Œª x y, R y x) v u.
Proof. induction 1 using vec_fall2_rect; auto with vec_db. Qed.






